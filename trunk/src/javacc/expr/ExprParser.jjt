//オプション定義
options{
    STATIC=false;
    MULTI=true;
    VISITOR=true;
    NODE_EXTENDS="javacc.expr.BaseNode";
}

//パーサークラスの定義
PARSER_BEGIN(ExprParser)
package javacc.expr.parser;
public class ExprParser{

}
PARSER_END(ExprParser)

//トークンの定義
SKIP:
{
    " " | "\r" | "\t" | "\n"
}
TOKEN:
{
        <PLUS : "+">
    |   <MINUS: "-">
    |	<MUL  : "*">
    |	<DIV  : "/">
    |	<LPAREN : "(">
    |	<RPAREN : ")">
    |   <INTEGER: (["0" - "9"])+>
}

//文法の定義
ASTStart Start():
{}
{
    AddExpr() { return jjtThis;}
}

void AddExpr() #void:
{}
{
    MulExpr() (
           <PLUS>  MulExpr() #Add(2)
        |  <MINUS> MulExpr() #Sub(2) ) *
}

void MulExpr() #void:
{}
{
	Value() (
			<MUL> Value() #Multi(2)
		|	<DIV> Value() #Division(2) )*
}

void Value() #void:
{}
{
		Integer()
	|	<LPAREN> AddExpr() <RPAREN>
}

void Integer():
{ Token t;}
{
    t = <INTEGER> { jjtThis.nodeValue = t.image;}
}