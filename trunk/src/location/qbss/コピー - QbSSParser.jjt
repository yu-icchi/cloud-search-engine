//---------------------------------------------------------
//QbSS (Query based Server Selection)
//---------------------------------------------------------

//オプション定義
options {
	STATIC=false;
	MULTI=true;
	VISITOR=true;
	NODE_EXTENDS="location.qbss.BaseNode";
	UNICODE_INPUT=true;
	JAVA_UNICODE_ESCAPE=true;
}

//パーサークラスの定義
PARSER_BEGIN(QbSSParser)
package location.qbss.parser;
public class QbSSParser {

}
PARSER_END(QbSSParser)

//トークンの定義
SKIP:
{
	" " | "\r" | "\t" | "\n"
}

TOKEN [IGNORE_CASE]:
{
		< OP_AND: "AND" >
	|	< OP_OR: "OR" >
	|	< OP_NOT: "NOT" >
}

TOKEN:
{
		< WORD: (~[" ", "\r", "\t", "\n", "\"", "(", ")"])+ >
}

//文法の定義
ASTStart Start():
{}
{
	Query() { return jjtThis; }
}

void Query() #void:
{}
{
	Word() (
			<OP_AND> Word() #And(2)
		|	<OP_OR> Word() #Or(2)
		|	<OP_NOT> Word() #Not(2) )*
}

void Word():
{ Token t; }
{
	t = <WORD> { jjtThis.nodeValue = t.image; }
}
