//---------------------------------------------------------
//QbSS (Query based Server Selection)
//---------------------------------------------------------

//オプション定義
options {
	STATIC=false;
	MULTI=true;
	VISITOR=true;
	NODE_EXTENDS="location.qbss.BaseNode";
	UNICODE_INPUT=true;
	JAVA_UNICODE_ESCAPE=true;
}

//パーサークラスの定義
PARSER_BEGIN(QbSSParser)
package location.qbss.parser;
public class QbSSParser {

}
PARSER_END(QbSSParser)

//トークンの定義
SKIP:
{
	" " | "\u3000" | "\r" | "\t" | "\n"
}

TOKEN [IGNORE_CASE]:
{
		< OP_AND: "AND" >
	|	< OP_OR: "OR" >
	|	< OP_NOT: "NOT" >
}

TOKEN:
{
		< WORD: (~[" ", "\u3000" , "\r", "\t", "\n", "\"", "(", ")"])+ >
	|	< LP: "(" >
	|	< RP: ")" >
	|	< QUOTE: "\"" >
}

//文法の定義
ASTStart Start():
{}
{
	Query() { return jjtThis; }
}

void Query() #void:
{}
{
	Value() (
			<OP_AND> Value() #And(2)
		|	<OP_OR> Value() #Or(2)
		|	<OP_NOT> Value() #Not(2) )*
}

void Value() #void:
{}
{
		Word()
	|	<QUOTE> (Word())+ <QUOTE> #Quote(>2)
	|	<LP> Query() <RP>
}

void Word():
{ Token t; }
{
	t = <WORD> { jjtThis.nodeValue = t.image; }
}
